// Player constants
ct Int PLAYER_MOVING_REST = 0
ct Int PLAYER_MOVING_RIGHT = 1
ct Int PLAYER_MOVING_LEFT = 2
ct Int PLAYER_SPEED_WALK = 2
ct Int PLAYER_SPEED_RUN = 4
ct Int GROUND_SPEED = -5
ct Int PLAYER_INITIAL_X = 128
ct Int PLAYER_INITIAL_Y = 128

// Player width and height
ct Int pw = 15
ct Int ph = 15
ct Int rw = pw - 1
ct Int rh = ph - 1
// Player characters
ct Int CURRENT_PLAYER_LUCI = 0
ct Int CURRENT_PLAYER_FER = 1
ct Int CURRENT_PLAYER_LUCIFER = 2

// Player data struct
struct Player
    SS x
    SSF y
    SF xspeed
    SF yspeed
    U movement
    U this_player
    AnimState anim_state
    U level_index
    Bool is_initialised
    SS camera_x

// Sprites and animations
data /sprites
    // luci metasprites
    [] sprite_luci_rest
        (make_metasprite(0, Ms{}(
            Ms(-8,-8, $54, $00),
            Ms( 0,-8, $55, $00),
            Ms(-8, 0, $64, $00),
            Ms( 0, 0, $65, $00)
        )))
    [] sprite_luci_right_1
        (make_metasprite(0, Ms{}(
            Ms(-8,-8, $54, $00),
            Ms( 0,-8, $55, $00),
            Ms(-8, 0, $64, $00),
            Ms( 0, 0, $65, $00)
        )))
    [] sprite_luci_right_2
        (make_metasprite(0, Ms{}(
            Ms(-8,-8, $56, $00),
            Ms( 0,-8, $57, $00),
            Ms(-8, 0, $66, $00),
            Ms( 0, 0, $67, $00)
        )))
    [] sprite_luci_left_1
        (make_metasprite(ATTR_H_FLIP, Ms{}(
            Ms(-8,-8, $54, $00),
            Ms( 0,-8, $55, $00),
            Ms(-8, 0, $64, $00),
            Ms( 0, 0, $65, $00)
        )))
    [] sprite_luci_left_2
        (make_metasprite(ATTR_H_FLIP, Ms{}(
            Ms(-8,-8, $56, $00),
            Ms( 0,-8, $57, $00),
            Ms(-8, 0, $66, $00),
            Ms( 0, 0, $67, $00)
        )))
    // luci animations
    [] luci_animation_rest
        (make_anim(Fs{}(
            Fs(20,   @sprite_luci_rest)
        )))
    [] luci_animation_right
        (make_anim(Fs{}(
            Fs(20,   @sprite_luci_right_1),
            Fs(20,   @sprite_luci_right_2)
        )))
    [] luci_animation_left
        (make_anim(Fs{}(
            Fs(20,   @sprite_luci_left_1),
            Fs(20,   @sprite_luci_left_2)
        )))

    // fer metasprites
    [] sprite_fer_rest
        (make_metasprite(0, Ms{}(
            Ms(-8,-8, $60, $01),
            Ms( 0,-8, $61, $01),
            Ms(-8, 0, $62, $01),
            Ms( 0, 0, $63, $01)
        )))
    [] sprite_fer_right_1
        (make_metasprite(0, Ms{}(
            Ms(-8,-8, $60, $01),
            Ms( 0,-8, $61, $01),
            Ms(-8, 0, $62, $01),
            Ms( 0, 0, $63, $01)
        )))
    [] sprite_fer_right_2
        (make_metasprite(0, Ms{}(
            Ms(-8,-8, $60, $01),
            Ms( 0,-8, $61, $01),
            Ms(-8, 0, $72, $01),
            Ms( 0, 0, $73, $01)
        )))
    [] sprite_fer_left_1
        (make_metasprite(ATTR_H_FLIP, Ms{}(
            Ms(-8,-8, $60, $01),
            Ms( 0,-8, $61, $01),
            Ms(-8, 0, $62, $01),
            Ms( 0, 0, $63, $01)
        )))
    [] sprite_fer_left_2
        (make_metasprite(ATTR_H_FLIP, Ms{}(
            Ms(-8,-8, $60, $01),
            Ms( 0,-8, $61, $01),
            Ms(-8, 0, $72, $01),
            Ms( 0, 0, $73, $01)
        )))
    // fer animations
    [] fer_animation_rest
        (make_anim(Fs{}(
            Fs(20,   @sprite_fer_rest)
        )))
    [] fer_animation_right
        (make_anim(Fs{}(
            Fs(20,   @sprite_fer_right_1),
            Fs(20,   @sprite_fer_right_2)
        )))
    [] fer_animation_left
        (make_anim(Fs{}(
            Fs(20,   @sprite_fer_left_1),
            Fs(20,   @sprite_fer_left_2)
        )))
    // lucifer metasprites
    [] sprite_lucifer_rest
        (make_metasprite(0, Ms{}(
            Ms(-8,-8, $00, $00),
            Ms(0, -8, $00, $00),
            Ms(-8, 0, $50, $00),
            Ms( 0, 0, $51, $00),
            Ms(-8, 8, $52, $00),
            Ms( 0, 8, $53, $00)
        )))
    [] sprite_lucifer_right_1
        (make_metasprite(0, Ms{}(
            Ms(-8, -8, $41, $00),
            Ms( 0, -8, $42, $00),
            Ms(-8,  0, $43, $00),
            Ms( 0,  0, $44, $00),
            Ms(-8,  8, $45, $00),
            Ms( 0,  8, $46, $00)
        )))
    [] sprite_lucifer_right_2
        (make_metasprite(0, Ms{}(
            Ms(-8, -8, $41, $00),
            Ms( 0, -8, $47, $00),
            Ms(-8,  0, $43, $00),
            Ms( 0,  0, $44, $00),
            Ms(-8,  8, $45, $00),
            Ms( 0,  8, $46, $00)
        )))
    [] sprite_lucifer_left_1
        (make_metasprite(ATTR_H_FLIP, Ms{}(
            Ms(-8, -8, $41, $00),
            Ms( 0, -8, $42, $00),
            Ms(-8,  0, $43, $00),
            Ms( 0,  0, $44, $00),
            Ms(-8,  8, $45, $00),
            Ms( 0,  8, $46, $00)
        )))
    [] sprite_lucifer_left_2
        (make_metasprite(ATTR_H_FLIP, Ms{}(
            Ms(-8, -8, $41, $00),
            Ms( 0, -8, $47, $00),
            Ms(-8,  0, $43, $00),
            Ms( 0,  0, $44, $00),
            Ms(-8,  8, $45, $00),
            Ms( 0,  8, $46, $00)
        )))

    // Animations
    [] lucifer_animation_rest
        (make_anim(Fs{}(
            Fs(20,   @sprite_lucifer_rest)
        )))
    [] lucifer_animation_right
        (make_anim(Fs{}(
            Fs(20,   @sprite_lucifer_right_1),
            Fs(20,   @sprite_lucifer_right_2)
        )))
    [] lucifer_animation_left
        (make_anim(Fs{}(
            Fs(20,   @sprite_lucifer_left_1),
            Fs(20,   @sprite_lucifer_left_2)
        )))

fn make_player(SS x, SS y) Player
    Player p = Player()
    p.x = x
    p.y = SSF(y)
    p.movement = PLAYER_MOVING_REST
    p.anim_state = AnimState()
    p.camera_x = 0
    return p

fn make_player_luci() Player
    Player p = make_player(PLAYER_INITIAL_X, PLAYER_INITIAL_Y)
    p.this_player = CURRENT_PLAYER_LUCI
    p.anim_state = make_anim_state(@luci_animation_rest)
    p.level_index = 2
    p.is_initialised = true
    return p

fn make_player_fer() Player
    Player p = make_player(PLAYER_INITIAL_X, PLAYER_INITIAL_Y)
    p.this_player = CURRENT_PLAYER_FER
    p.anim_state = make_anim_state(@fer_animation_rest)
    p.level_index = 3
    p.is_initialised = true
    return p

fn save_player(Player a) Player
    Player p = Player()
    p.x = a.x
    p.y = a.y
    p.xspeed = a.xspeed
    p.yspeed = a.yspeed
    p.movement = a.movement
    p.anim_state = a.anim_state
    p.level_index = a.level_index
    p.is_initialised = a.is_initialised
    p.camera_x = a.camera_x
    return p

fn restore_player(Player a) Player
    Player p = Player()
    p.x = a.x
    p.y = a.y
    p.xspeed = a.xspeed
    p.yspeed = a.yspeed
    p.movement = a.movement
    p.anim_state = a.anim_state
    p.level_index = a.level_index
    p.this_player = a.this_player
    p.is_initialised = a.is_initialised
    p.camera_x = a.camera_x
    return p

fn update_player()
    // Select - Change player
    if is_select_pressed && current_player != CURRENT_PLAYER_LUCIFER
        is_select_pressed = false
        // Change player
        if current_player == CURRENT_PLAYER_LUCI
            current_player = CURRENT_PLAYER_FER
            luci = save_player(p)
            p = restore_player(fer)
        else if current_player == CURRENT_PLAYER_FER
            current_player = CURRENT_PLAYER_LUCI
            fer = save_player(p)
            p = restore_player(luci)
        // Change level
        current_level_index = p.level_index
        goto mode play_level(current_level_index)
        : preserves /game

    // Update x-position:
    SS speed = PLAYER_SPEED_WALK
    if pads[0].held & BUTTON_B && current_player != CURRENT_PLAYER_LUCIFER
        speed = PLAYER_SPEED_RUN
    if pads[0].held & BUTTON_LEFT
        if collision_at(UU(p.x - speed), UU(p.y)) != C_WALL && collision_at(UU(p.x - speed), UU(p.y + rh)) != C_WALL
            if p.movement != PLAYER_MOVING_LEFT
                p.movement = PLAYER_MOVING_LEFT
                switch current_player 
                    case CURRENT_PLAYER_LUCI
                        p.anim_state = make_anim_state(@luci_animation_left)
                        break
                    case CURRENT_PLAYER_FER
                        p.anim_state = make_anim_state(@fer_animation_left)
                        break
            p.x -= speed
    if pads[0].held & BUTTON_RIGHT
        if collision_at(UU(p.x + rw + speed), UU(p.y)) != C_WALL && collision_at(UU(p.x + rw + speed), UU(p.y + rh)) != C_WALL
            if p.movement != BUTTON_RIGHT
                p.movement = BUTTON_RIGHT
                switch current_player
                    case CURRENT_PLAYER_LUCI
                        p.anim_state = make_anim_state(@luci_animation_right)
                        break
                    case CURRENT_PLAYER_FER
                        p.anim_state = make_anim_state(@fer_animation_right)
                        break
            p.x += speed
    
    // Ground and Ladder
    SS center_x = SS(p.x) + (pw >> 1)
    SS center_y = SS(p.y) + (ph >> 1)
    // Ground
    if current_player == CURRENT_PLAYER_FER || (collision_at(UU(center_x), UU(center_y + PLAYER_SPEED_WALK)) != C_LADDER && collision_at(UU(center_x), UU(center_y + ph - PLAYER_SPEED_WALK)) != C_LADDER)
        if collision_at(UU(p.x + 0), UU(p.y + ph)) == C_WALL || collision_at(UU(p.x + rw), UU(p.y + ph)) == C_WALL
            if pads[0].pressed & BUTTON_A
                if current_player != CURRENT_PLAYER_LUCI
                    p.yspeed = GROUND_SPEED

        // Update y-position:
        SSF new_y = p.y + SSF(p.yspeed)
        p.yspeed += 0.25
        if p.yspeed > 0
            if collision_at(UU(p.x + 0), UU(new_y + rh)) == C_WALL || collision_at(UU(p.x + rw), UU(new_y + rh)) == C_WALL
                p.yspeed = 0
                p.y = ((new_y + rh) & SSF(~%1111)) - ph
            else
                p.y = new_y
        else
            if collision_at(UU(p.x + 0), UU(new_y)) == C_WALL || collision_at(UU(p.x + rw), UU(new_y)) == C_WALL
                p.yspeed = 0
                p.y = (p.y & SSF(~%1111))
            else
                p.y = new_y
    // Ladder
    else
        if pads[0].held & BUTTON_UP
            if collision_at(UU(p.x), UU(p.y - ph - PLAYER_SPEED_WALK)) != C_WALL
                p.y += GROUND_SPEED + PLAYER_SPEED_WALK
        if pads[0].held & BUTTON_DOWN
            if collision_at(UU(p.x), UU(p.y + ph + PLAYER_SPEED_WALK)) != C_WALL
                p.y -= GROUND_SPEED + PLAYER_SPEED_WALK

    // Resting
    if !pads[0].held && p.movement != PLAYER_MOVING_REST
        p.movement = PLAYER_MOVING_REST
        switch current_player
            case CURRENT_PLAYER_LUCI
                p.anim_state = make_anim_state(@luci_animation_rest)
                break
            case CURRENT_PLAYER_FER
                p.anim_state = make_anim_state(@fer_animation_rest)
                break

    // Check for coin/exit collisions:
    switch collision_at(UU(center_x), UU(center_y)) 
        case C_COIN
            destroy(UU(center_x), UU(center_y))
            break
        case C_EXIT
            current_level_index += 1
            if current_level_index >= len(levels)
                current_level_index = 2
            // Update player
            p.level_index = current_level_index
            p.x = PLAYER_INITIAL_X
            p.y = PLAYER_INITIAL_Y
            switch current_player
                case CURRENT_PLAYER_LUCI
                    luci = save_player(p)
                    break
                case CURRENT_PLAYER_FER
                    fer = save_player(p)
                    break
            
            if current_level_index >= len(levels)
                current_level_index = 2
            nmi
            goto mode play_level(current_level_index)
            : preserves /game


    // Update camera:
    ct Int window = 112
    if center_x - p.camera_x < window
        p.camera_x = center_x - window
    else if center_x - p.camera_x > 256 - window
        p.camera_x = center_x - (256 - window)
    p.camera_x = min(max(0, p.camera_x), (SS(current_level_width) << 4) - 256)